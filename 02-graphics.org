#+TITLE: Stata : fonctionnalités graphiques
#+LANGUAGE: fr
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="worg.css" />
#+HTML_MATHJAX: scale: 90
#+OPTIONS: H:3 num:nil toc:t \n:nil ':t @:t ::t |:t ^:nil -:t f:t *:t TeX:t skip:nil d:nil html-style:nil html-postamble:nil tags:not-in-toc

* Introduction

Stata dispose d’un excellent système graphique avec de nombreuses options de personnalisation. La référence reste l’ouvrage de Mitchell, actuellement dans sa 3e édition cite:mitchell-2004-visual-guide. D'autres ouvrages sont disponibles, comme par exemple celui de Nick Cox cite:cox-2014-speak-stata-graph. Mais il est également recommendé de consulter le [[http://www.stata.com/support/faqs/graphics/gph/stata-graphs/][site de support]] de Stata qui offre un panorama des différents types de graphique avec un accès interactif à chacun des cas de figure.

Les commandes graphiques peuvent être utilisées pour décrire des données ou en tant que commandes de "post estimation" (par exemple, analyse en composantes principales, graphique de résidus).

Les données qui serviront de base à la plupart des illustrations de ce chapitre portent sur une enquête socio-économique allemande réalisée en 2009 et décrite dans l'ouvrage de Kohler & Kreuter cite:kohler-2012-data-analy. Les avriables d'intérêt sont les suivantes : =ybirth= (année de naissance), =hhnr2009= (foyer résidentiel), =sex= (sexe), =mar= (statut marital), =edu= (niveau d’éducation), =yedu= (nombre d’années de formation), =voc= (niveau secondaire ou université), =emp= (type d’emploi), =egp= (catégorie socio professionnelle), =income= (revenus, en €), =size= (taille du logement), =hhsize= (nombre de personnes dans habitation).

#+BEGIN_SRC stata :session :results output :exports both
set scheme plotplain
use data/gsoep09
describe, short
describe, simple
#+END_SRC

Voici un aperçu des données sous forme de listing des premières observations :

#+BEGIN_SRC stata :session :results output :exports both
list persnr-sex in 1/5
#+END_SRC



* Graphiques univariés de base


** Fonction de répartition et fractiles

Considérons l'âge des répondants qui peut être reconstruit à partir de l'année de naissance (=ybirth=) et de l'année de réalisation de l'enquête (2009) :

#+BEGIN_SRC stata :session :results output :exports both
generate age = 2009 - ybirth
summarize age
#+END_SRC

Bien entendu, l'option =details= de [[stata:summarize][summarize]] apporte beaucoup plus d'information concernant la distribution de la variable, notamment du point de vue des fractiles :
#+BEGIN_SRC stata :session :results output :exports both
summarize age, details
#+END_SRC

Pour se représenter ces chiffres sous forme graphique, il est possible d'afficher la fonction de répartition empirique des données, $F_X(x)$, définie comme $F_X(x) = P(X \le x)$ pour une variable aléatoire $X$, ce qui permet d'évaluer visuellement quelle proportion des données se situe en-deça d'une certaine valeur $x$. Il est également possible de jauger de la quantité d'observations contenues dans un intervalle (puisque $P(a < X \le b) = F_X(b) - F_X(a)$), surtout si l'on s'aide visuellement en superposant des lignes horizontales au points d'ordonnée $a$ et $b$.

Dans un premier temps, examinons la courbe des quantiles produite par la commande [[stata:quantile][quantile]]. Il s'agit de représenter la distribution jointe des valeurs ordonnées de la variable d'intérêt en fonction des quantiles d'une loi uniforme :

#+BEGIN_SRC stata :session :results output :exports code
quantile age
graph export "fig-02-quantile-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution des fractiles d'âge
#+NAME:      fig:02-quantile-age
#+LABEL:     fig:02-quantile-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-quantile-age.png]]

Pour la fonction de répartition, si l'on ne souhaite pas passer par des packages additionnels (p.ex., [[https://ideas.repec.org/c/boc/bocode/s456409.html][cdfplot]]), le plus simple consiste à calculer directement à calculer la fonction de répartition empirique à l'aide de [[stata:cumul][cumul]]. Il s'agit en réalité de la fonction réciproque de celle présentée plus haut. Dans l'exemple ci-après, on affiche cette variable auxiliaire en fonction de l'âge et on surligne le premier et le troisième quartile par des lignes horizontales : 

#+BEGIN_SRC stata :session :results output :exports code
cumul age, gen(agecum)
sort agecum
graph twoway line agecum age, ytitle(F(x)) yline(.25 .75, lwidth(thin))
graph export "fig-02-cumul-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Fonction de répartition empirique de l'âge
#+NAME:      fig:02-cumul-age
#+LABEL:     fig:02-cumul-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-cumul-age.png]]

Un graphique tout aussi intéressant consiste à afficher les fractiles d'une variable en fonction des fractiles d'une loi normale, ce que l'on appelle communément un diagramme de type quantile-quantile. L'examen de la forme de la courbe permet d'évaluer rapidement si les données sont compartibles avec l'hypothèse selon laquelle elles se distribuent "à peu près normalement".

#+BEGIN_SRC stata :session :results output :exports code
qnorm age, grid
graph export "fig-02-qnorm-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Diagramme quantile-quantile pour l'âge
#+NAME:      fig:02-qnorm-age
#+LABEL:     fig:02-qnorm-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-qnorm-age.png]]


** Histogrammes et courbes de densité


Pour construire un histogramme de cette variable, on utilisera la commande [[stata:histogram][histogram]], en précisant l'option =freq= si l'on souhaite afficher les effectifs ("fréquences" en anglais) plutôt que la densité :

#+BEGIN_SRC stata :session :results output :exports code
histogram age, xtitle("Age des participants")
graph export "fig-02-histogram-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution de l'âge des répondants
#+NAME:      fig:02-histogram-age
#+LABEL:     fig:02-histogram-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-histogram-age.png]]

Notons que cette commande dispose de l'option =by()= des commandes [[stata:twoway][twoway]] de sorte qu'il est assez simple de tracer des histogrammes conditionnés sur les valeurs prise par une autre variable, comme illustré ci-après.

#+BEGIN_SRC stata :session :results output :exports code
label variable age "Age des participants"
histogram age, bin(15) by(sex)
graph export "fig-02-histogram-age-sex.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution de l'âge des répondants selon le sexe
#+NAME:      fig:02-histogram-age-sex
#+LABEL:     fig:02-histogram-age-sex
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-histogram-age-sex.png]]


Une courbe de densité empirique

Rappelons que la fonction de densité, $f_X$, et la fonction de répartition, $F_X(x)$, discutée plus haut sont liées par une relation simple. Dans le cas continu, $F_X(x) = \int_{-\infty}^x f_X(t)\, \mathrm{d}t$.

#+BEGIN_SRC stata :session :results output :exports code
kdensity age, normal bw(5) lwidth(0.8)
graph export "fig-02-kdensity-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution de l'âge des répondants selon le sexe
#+NAME:      fig:02-kdensity-age
#+LABEL:     fig:02-kdensity-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-kdensity-age.png]]

#+BEGIN_SRC stata :session :results output :exports code
graph twoway kdensity age, by(sex)
graph export "fig-02-kdensity-age-sex.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution de l'âge des répondants selon le sexe
#+NAME:      fig:02-kdensity-age-sex
#+LABEL:     fig:02-kdensity-age-sex
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-kdensity-age-sex.png]]


** Diagrammes en barres et en points

Bien qu'il existe une commande graphique pour tracer des diagrammes en barres, [[stata:graph bar][graph bar]], celle-ci s'utilise généralement à partir de données agrégées (voir l'option =yvars= avec =(stat)=). On utilise donc la même commande qu'à la section précédente, [[stata:histogram][histogram]], en spécifiant l'option =discrete=, comme illustré ci-dessous :

#+BEGIN_SRC stata :session :results output :exports code
histogram edu, discrete freq addlabels
graph export "fig-02-histogram-edu.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution d'effectifs selon les niveaux d'éducation
#+NAME:      fig:02-histogram-edu
#+LABEL:     fig:02-histogram-edu
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-histogram-edu.png]]

Comme on peut le voir, la figure [[fig:02-histogram-edu]] est loin d'être parfaite. En particulier, les libellés de l'axe des abscisses sont peu lisibles (à moins d'ajouter une légende indiquant la correspondance entre les codes numériques et les étiquettes des niveaux de la variable =edu=), et les barres verticales sont juxtaposées de sorte qu'avec l'axe numérique il est facile de prendre ce diagramme pour un histogramme pour données continues. Voici une version qui corrige ces deux aspects, notamment grâce à l'usage de =gap()= et une spécification plus poussée des étiquettes de l'axe des x :

#+BEGIN_SRC stata :session :results output :exports code
histogram edu, discrete freq addlabels gap(10) xla(1/5, valuelabel noticks ang(45))
graph export "fig-02-histogram-edu-v2.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution d'effectifs selon les niveaux d'éducation
#+NAME:      fig:02-histogram-edu-v2
#+LABEL:     fig:02-histogram-edu-v2
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-02-histogram-edu-v2.png]]

Pour réaliser un tel diagramme en utilisant [[stata:graph bar][graph bar]], il est nécessaire de travailler avec une variable auxiliaire dans laquelle on accumule les effectifs conditionnels.

#+BEGIN_SRC stata :session :results no :exports code
graph bar (count) persnr, over(edu) asyvars percent showyvars bargap(10) ytitle("Proportion") legend(off)
#+END_SRC



#+BIBLIOGRAPHY: references nil limit:t option:-nobibsource

