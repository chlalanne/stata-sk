#+TITLE: Stata : modélisation statistique (1)
#+LANGUAGE: fr
#+HTML_HEAD: <link rel="stylesheet" type="text/css" href="worg.css" />
#+HTML_MATHJAX: scale: 90
#+OPTIONS: H:3 num:nil toc:t \n:nil ':t @:t ::t |:t ^:nil -:t f:t *:t TeX:t skip:nil d:nil html-style:nil html-postamble:nil tags:not-in-toc

Dans ce chapitre, on s'intéressera à la construction de modèles de régression explicatifs ou prédictifs. La première partie se concentre sur le modèle linéaire et ses applications. La seconde partie porte sur le modèle linéaire généralisé. Il existe un très bon ouvrage traitant du modèle linéaire généralisé, à présent dans sa quatrième édition et publié chez Stata Press : [[https://www.stata.com/bookstore/generalized-linear-models-and-extensions/][Generalized Linear Models and Extensions]] de Hardin & Hilbe. Concernant la modélisation statistique en général, les ouvrages de Vittinghoff et coll. cite:vittinghoff-2005-regres-method-biost et Dupont cite:dupont-2009-statis-model couvrent l'essentiel des notions présentées. Pour aller plus loin, le livre de Harrell cite:harrell-2015-regres-model-strat demeure une référence en ce qui concerne les techniques biostatistiques modernes appliquées dans un contexte médical. Bien que reposant sur le logiciel R, la plupart des techniques discutées dans cet ouvrage sont disponibles sous Stata.

La mise en oeuvre d'un modèle de régression a déjà été discutée brièvement dans le [[./00-intro.html][tutoriel d'introduction à Stata]]. Dans ce chapitre, on va s'intéresser à l'estimation des paramètres d'un modèle de régression linéaire, à la sélection de modèle dans un cadre explicatif, au diagnostic du modèle, et à la prédiction ponctuelle ou par intervalles. On prendra pour base des données observationnelles issues d'enquêtes ou d'études cliniques transversales. Les séries chronologiques et les données longitudinales seront traitées dans des chapitres séparés.

* Le modèle de régression linéaire simple

Dans un premier temps, procédons à quelques rappels concernant la régression linéaire simple, la corrélation linéaire et le test de Student. Les notions connexes telles que les associations non linéaires ou les approches non paramétriques seront traitées ultérieurement. 

** Un exemple de régression linéaire simple

Les données d'illustration peuvent être chargées directement depuis internet à l'aide de la commande [[stata:webuse][webuse]]. Il s'agit d'une enquête épidémiologique rétrospective dans laquelle on s'intéresse aux facteurs de risque d'un bébé ayant un poids inférieur à la norme, selon les normes américaines des années 90. Ces données sont extensivement analysées dans l'ouvrage de Hosmer & Lemeshow cite:hosmer-2000-applied-logis-regres. Au total, le tableau de données contient 11 variables, la variable réponse étant le poids des bébés (=bwt=) :

#+BEGIN_SRC stata :session :results output :exports both
webuse lbw
describe, simple
#+END_SRC

#+RESULTS:
: set more off
: webuse lbw
: (Hosmer & Lemeshow data)
: describe, simple
: id     low    age    lwt    race   smoke  ptl    ht     ui     ftv    bwt

La relation entre le poids des bébés (en grammes) et le poids des mères (initialement en livres, converti en kilogrammes) est représentée dans la figure suivante sous la forme d'un simple diagramme de dispersion. Pour faciliter la lecture, le poids des mères est converti en kilogrammes, sans modifier le mode de représentation de la variable :

#+BEGIN_SRC stata :session :results output :exports both
replace lwt = lwt / 2.204623, nopromote
#+END_SRC

Ensuite, on utilise la commande [[stata:scatter][scatter]] pour construire le diagramme de dispersion :

#+BEGIN_SRC stata :session :results output :exports code
set scheme plotplain
graph twoway scatter bwt lwt
graph export "fig-03-scatter-bwt-lwt.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre le poids des bébés et le poids des mères
#+NAME:      fig:03-scatter-bwt-lwt
#+LABEL:     fig:03-scatter-bwt-lwt
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-scatter-bwt-lwt.png]]

Il est tout à fait possible et largement recommendé de superposer une courbe loess cite:cleveland-1979-robus-local sur le diagramme de dispersion précédent afin d'évaluer visuellement les écarts à la linéarité concernant la relation entre ces deux variables. La commande [[stata:lowess][lowess]] peut être combinée à [[stata:scatter][scatter]] :

#+BEGIN_SRC stata :session :results output :exports code
graph twoway scatter bwt lwt || lowess bwt lwt, legend(off)
graph export "fig-03-lowess-bwt-lwt.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre le poids des bébés et le poids des mères (courbe loess)
#+NAME:      fig:03-lowess-bwt-lwt
#+LABEL:     fig:03-lowess-bwt-lwt
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-lowess-bwt-lwt.png]]

La corrélation entre ces deux variables s'obtient grâce à [[stata:correlate][correlate]]. Notons que cette commande fonctionne avec deux variables ou une liste de variables de sorte qu'elle pourra également être utilisée pour construire une matrice de corrélation :

#+BEGIN_SRC stata :session :results output :exports both
summarize bwt lwt
correlate bwt lwt
#+END_SRC

Voici une formulation simplifiée du modèle de régression linéaire. Soit $y_i$ la réponse observée sur l'individu $i$, et $x_i$ sa valeur
observée pour le prédicteur $x$. Le modèle de régression linéaire s'écrit :
$$y_i = \beta_0+\beta_1x_i+\varepsilon_i,$$
où $\beta_0$ représente l'ordonnée à l'origine (/intercept/) et $\beta_1$ la pente (\emph{slope}) de la droite de régression, et
$\varepsilon_i\sim\mathcal{N}(0,\sigma^2)$ est un terme d'erreur (résidus, supposés indépendants). En minimisant les différences quadratiques entre les valeurs observées et les valeurs prédites (principe des MCO), on peut estimer les coefficients de régression, $\widehat\beta_0$ et $\widehat\beta_1$ :
$$\begin{array}{l}
\widehat\beta_0 = \bar y - \widehat\beta_1\bar x\\
\widehat\beta_1 = \sum(y_i-\bar y)(x_i-\bar x)/\sum(x_i-\bar x)^2\\
\end{array}$$

Sous $H_0$, le rapport entre l'estimé de la pente ($\widehat\beta_1$, de variance $\frac{\text{SSR}/(n-2)}{(n-1)s_x^2}$) et son erreur standard suit une loi de Student à $(n-2)$ degrés de liberté.

Les paramètres d'un tel modèle de régression, $\widehat\beta_0$ et $\widehat\beta_1$, peuvent être estimés grâce à la commande [[stata:regress][regress]], en indiquant la variable à prédire et la ou les variables explicatives. Pour un modèle de régression linéaire simple, on se retrouve donc avec l'expression la plus simple qui soit :

#+BEGIN_SRC stata :session :results output :exports both
regress bwt lwt
#+END_SRC

Les résultats fournis par [[stata:regress][regress]] se composent de deux tableaux : le tableau d'analyse de variance du modèle de régression, qui peut être supprimé via l'option =noheader=, et le tableau des coefficients de régression. La ligne =_cons= désigne le terme d'ordonnée à l'origine, $\widehat\beta_0$. Le résultat du test de Student associé à =lwt= ($\widehat\beta_1$) peut se retrouver manuellement une fois que l'on a extrait les valeurs d'intérêt :

#+BEGIN_SRC stata :session :results output :exports both
local tstat = _b[lwt] / _se[lwt]
display "t = " %4.2f `tstat' " p = " %4.3f 2*ttail(187, `tstat')
#+END_SRC
 
** Diagnostic du modèle

La commande [[stata:predict][predict]] permet non seulement de calculer les valeurs ajustées du modèle mais également les résidus du modèle ($e_i = \tilde y_i - y_i$) ainsi que d'autres statistiques utiles pour diagnostiquer la qualité d'ajustement du modèle de régression. 

#+BEGIN_SRC stata :session :results output :exports both
predict double yhat
predict double rs, rstudent
summarize rs
#+END_SRC

Dans le cas ci-dessus, ce sont les [[https://onlinecourses.science.psu.edu/stat462/node/247/][résidus studentisés]], $r_i = e_i / (s_{(i)}\sqrt{1-h_i})$, qui ont été calculés. D'autres options sont également disponibles mais ce type de résidus facilite à la fois l'interprétation et la détection de valeurs extrêmes (voir également l'aide en ligne, =help regress postestimation=). Par exemple, voici un diagramme de quantiles pour les résidus simples :

#+BEGIN_SRC stata :session :results output :exports code
predict double r, resid
qnorm r
graph export "fig-03-qnorm-r.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution des résidus simples 
#+NAME:      fig:03-qnorm-r
#+LABEL:     fig:03-qnorm-r
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-qnorm-r.png]]

Un histogramme ou une courbe de densité permet également d'examiner rapidement la forme de la distribution des résidus. Voici un exemple avec [[stata:kdensity][kdensity]], pour lequel une courbe de densité normale a été ajoutée à l'aide de l'option =normal= :

#+BEGIN_SRC stata :session :results output :exports code
kdensity rs, normal normopts(lpat(--))
graph export "fig-03-kdensity-rs.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Distribution des résidus studentisés
#+NAME:      fig:03-kdensity-rs
#+LABEL:     fig:03-kdensity-rs
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-kdensity-rs.png]]

Le graphique suivant est plus informatif car il renseigne à la fois sur la distribution des résidus et la corrélation entre les valeurs prédites par le modèle et ces derniers, qui selon l'hypothèse du modèle doit être nulle. Ici, on utilise les valeurs de post-estimation calculées plus haut, mais il serait tout à fait possible d'utiliser directement la commande de post-estimation [[stata:rvfplot][rvfplot]] (ou [[stata:rvpplot][rvpplot]], mais dans le cas d'une régression avec un seul prédicteur cela ne change rien) :

#+BEGIN_SRC stata :session :results output :exports code
graph twoway scatter rs yhat, yline(0)
graph export "fig-03-scatter-rs-yhat.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre valeurs ajustées et résidus
#+NAME:      fig:03-scatter-rs-yhat
#+LABEL:     fig:03-scatter-rs-yhat
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-scatter-rs-yhat.png]]

** Cas de la régression sur une variable catégorielle

On a vu dans le chapitre sur la [[./01-data.html][gestion des données]] comment représenter les variables catégorielles sous Stata : dans le cas des variables binaires, un codage sous forme de 0 et de 1 est parfaitement adéquat, tandis que dans le cas des variables à plus de deux modalités, on assigne à chaque niveau un code numérique en débutant à 1. Ainsi, pour une variable à trois modalités, le premier niveau sera représenté par la valeur 1 tandis que le troisième et dernier niveau prendra la valeur 3. On associera éventuellement des étiquettes à chacun des niveaux afin de mieux identifier les différentes classes.

Considérons la variable =smoke= qui indique si la mère fumait pendant le premier trimestre de sa grossesse :

#+BEGIN_SRC stata :session :results output :exports both
tabulate smoke, nolabel
tabstat bwt, by(smoke) stat(mean sd n)
#+END_SRC


#+BEGIN_SRC stata :session :results output :exports code
graph box bwt, over(smoke)
graph export "fig-03-box-bwt-smoke.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et statut fumeur
#+NAME:      fig:03-box-bwt-smoke
#+LABEL:     fig:03-box-bwt-smoke
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-box-bwt-smoke.png]]

Le modèle de régression suivant considère la variable =smoke= comme une variable numérique et le coefficient de régression pour cette variable représente la variation de poids lorsque =smoke= varie d'une unité (de 0 à 1) :

#+BEGIN_SRC stata :session :results output :exports both
regress bwt smoke
#+END_SRC

En indiquant à Stata que la variable =smoke= doit être traitée comme une variable catégorielle et de générer l'ensemble de variables indicatrices correspondant, on obtiendra strictement le même résultat du fait du codage initial en 0/1 :

#+BEGIN_SRC stata :session :results output :exports both
regress bwt i.smoke
#+END_SRC

Considérons la variable =race= qui a trois niveaux. Il est tout à fait possible de générer l'ensemble des indicatrices associées à cette variable à l'aide de [[stata:tabulate][tabulate]] :

#+BEGIN_SRC stata :session :results output :exports both
quietly tabulate race, gen(irace)
list race irace* in 1/5
#+END_SRC

Ensuite, il suffira d'inclure deux indicatrices parmi les trois dans le modèle de régression, par exemple =regress bwt irace2 irace3=. Mais comme on l'a vu plus haut, l'opérateur =i.= permet de générer automatiquement un ensemble d'indicatrices pour n'importe quelle variable catégorielle :

#+BEGIN_SRC stata :session :results output :exports both
regress bwt i.race
#+END_SRC

Par défaut, le premier niveau de la variable catégorielle (ici, =white=) sert de niveau de référence, mais il est tout à fait possible de modifier ce comportement en indiquant la catégorie à utiliser pour la "baseline". En utilisant le préfixe =ib3=, on indique à Stata que le troisième niveau de =race= servira de catégorie de référence :

#+BEGIN_SRC stata :session :results output :exports both
regress bwt ib3.race
#+END_SRC

On retrouvera bien les différences de moyennes par simple estimation de contrastes grâce à [[stata:contrast][contrast]] ou [[stata:margins][margins]] :

#+BEGIN_SRC stata :session :results output :exports both
contrast r.race, nowald effects
#+END_SRC


** Lien avec le test de Student

La différence de moyennes utilisée pour former la statistique de test de Student et qui est rappelée dans la sortie de [[stata:ttest][ttest]] ci-dessous correspond strictement à la pente de la droite de régression estimée dans la section précédente :

#+BEGIN_SRC stata :session :results output :exports both
ttest bwt, by(smoke)
#+END_SRC

On peut d'ailleurs visualiser très facilement ce différentiel de moyennes à l'aide d'un simple diagramme de dispersion en considérant la variable binaire sur l'axe des abscisses. Plutôt que d'utiliser [[stata:scatter][scatter]] et de redéfinir l'axe des x, il est plus simple d'utiliser un diagramme un point tel que proposé par la commande externe =stripplot= (à installer au préalable, =ssc install stripplot=) :

#+BEGIN_SRC stata :session :results output :exports code
stripplot bwt, over(smoke) vertical jitter(1 0) addplot(lfit bwt smoke)
graph export "fig-03-stripplot-bwt-smoke.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et statut fumeur
#+NAME:      fig:03-stripplot-bwt-smoke
#+LABEL:     fig:03-stripplot-bwt-smoke
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-stripplot-bwt-smoke.png]]

Une manière de vérifier graphiquement l'hypothèse d'égalité des variances, nécessaire dans le test ci-dessus afin de recouvrer les résultats du test du coefficient de régression, consisterait à comparer les fonctions de répartition empirique des deux groupes comme suggéré sur le [[https://www.statalist.org/forums/forum/general-stata-discussion/general/1322693-how-to-visualize-independent-two-sample-t-tests][forum Stata]].

Dans le cas d'une variable catégorielle à plus de deux niveaux, telle que =race=, il est toujours possible de former l'ensemble des tests de Student pour la comparaison des différentes paires de moyennes à l'aide de [[stata:pwmean][pwmean]] comme illsutré ci-dessous :

#+BEGIN_SRC stata :session :results output :exports both
pwmean bwt, over(race) effects
#+END_SRC

L'option =mcompare()= permet d'adapter le type de statistique de test (Tukey, Dunnett), mais dans le cas du modèle de régression précédent il n'y a pas lieu d'appliquer de correction pour les tests multiples ou de modifier la statistique de test. La commande [[stata:pwmean][pwmean]] fournit les mêmes résultats et accepte les mêmes options que [[stata:pwcompare][pwcompare]]. La seule différence est qye cette dernière s'utilise en tant que commande de post-estimation et sa syntaxe est plus souple dans le cas des modèles à plusieurs prédicteurs, incluant d'éventuels termes d'interaction.


** Traitement de la non linéarité

Il existe plusieurs approches pour traiter le cas d'une relation non linéaire entre la variable réponse et un prédicteur continu.

Voici une illustration avec l'âge de la mère :

#+BEGIN_SRC stata :session :results output :exports code
scatter bwt age || qfitci bwt age, legend(off)
graph export "fig-03-scatter-bwt-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et âge de la mère
#+NAME:      fig:03-scatter-bwt-age
#+LABEL:     fig:03-scatter-bwt-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-scatter-bwt-age.png]]

L'estimation des paramètres du modèle de régression ne pose pas de difficulté lorsque l'on suppose une simple relation linéaire :

#+BEGIN_SRC stata :session :results output :exports both
gen agesq = age^2
regress bwt age agesq
#+END_SRC

L'approche qui suit repose sur des polynômes fractionnaires, qui ont été largement développés et popularisés par Royston et coll. cite:royston-1994-regres-using. L'idée générale est de considérer des polynômes dont les exposants sont pris dans un ensemble prédéfini de valeurs $P = {-2, -1, -0.5, 0, 0.5, 1, 2, 3}$, où par convention $x^{(0)} = \ln(x)$. Un polynôme fractionnaire de degré $m$ se construit comme $\text{FPm} = \beta_0 + \sum_{j=1}^m \beta_jx^{(p_j)}$, où $p_j \in P$. On notera que pour un polynôme de degré $m$, une même puissance peut être répétée $m$ fois.

Stata 13 disposait de la commande [[stata:fracpoly][fracpoly]] mais désormais il est recommendé d'utiliser les commandes [[stata:fp][fp]] (cas univarié) et [[stata:mfp][mfp]] (cas multivarié) qui permettent de construire automatiquement les termes d'un ou plusieurs polynômes fractionnaires pour une variable numérique donnée. Voici un exemple d'application sur la variable =age= :

#+BEGIN_SRC stata :session :results output :exports both
fp <age> : regress bwt <age>
#+END_SRC

#+BEGIN_SRC stata :session :results output :exports code
fp plot, residuals(none)
graph export "fig-03-fpplot-bwt-age.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Utilisation de polynômes fractionnaires pour la relation entre poids des bébés et âge de la mère
#+NAME:      fig:03-fpplot-bwt-age
#+LABEL:     fig:03-fpplot-bwt-age
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-fpplot-bwt-age.png]]


** Approche robuste

*FIXME* Find a better illustration + provide more background

Plutôt que de minimiser les écarts quadratiques entre les valeurs prédites et les valeurs observées, il est tout à fait possible d'utiliser un autre type d'estimateur.

Considérons la relation entre le poids des bébés et le poids des mères dont l'ethnicité est =black=. La commande suivante permet d'afficher un simple diagramme de dispersion ainsi que la droite de régression associée :

#+BEGIN_SRC stata :session :results output :exports code
twoway (scatter bwt lwt) (lfit bwt lwt) if race == 3
graph export "fig-03-scatter-bwt-lwt-race3.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Relation entre poids des bébés et taille de la mère
#+NAME:      fig:03-scatter-bwt-lwt-race3
#+LABEL:     fig:03-scatter-bwt-lwt-race3
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-scatter-bwt-lwt-race3.png]]

Les valeurs ajustées du modèle de régression peuvent être obtenues à l'aide de [[stata:predict][predict]] :

#+BEGIN_SRC stata :session :results output :exports both
regress bwt lwt if race == 3
predict yhols
#+END_SRC

La commande =robreg= disponible dans le package du même nom (=ssc install moremata robreg=) permet d'estimer les paramètres d'un modèle linéaire en utilisant des M-estimateurs (Huber ou bisquare) cite:jann-2010-robreg. Dans le cas d'une approche par M-estimation, les estimés des paramètres du modèle de régression sont obtenus en minimisant une fonction de coût, $\rho$, reposant sur la valeur des résidus sur l'ensemble des valeurs de $X$. Spécifiquement, on recherche une fonction $\rho(e) \ge 0$, symétrique et monotone, 

La syntaxe est identique à celle de [[stata:regress][regress]] mais il faut faut préciser le type d'estimateur après le nom de la commande : =robreg m= signifie par exemple une régression avec un estimateur de Huber tandis que =robreg s= indique à Stata d'utiliser un S-estimateur. Un exemple d'application est disponible dans cite:vittinghoff-2005-regres-method-biost (*FIXME* check the reference carefully). Dans le cas présent, on utilisera l'instruction suivante :

#+BEGIN_SRC stata :session :results output :exports code
quietly robreg m bwt lwt if race == 3
predict yhm
#+END_SRC

On peut superposer les prédictions de ces deux modèles sur le diagramme de dispersion précédent comme illustré ci-dessous :

#+BEGIN_SRC stata :session :results output :exports code
twoway (scatter bwt lwt if race == 3) (line yhols yhm lwt, lwidth(*2 *2)), legend(order(2 "OLS" 3 "Huber"))
graph export "fig-03-scatter-bwt-lwt-race3-2.pdf", fontface(DroidSans) replace
#+END_SRC

#+CAPTION:   Estimation MCO versus M-estimateur
#+NAME:      fig:03-scatter-bwt-lwt-race3-2
#+LABEL:     fig:03-scatter-bwt-lwt-race3-2
#+ATTR_HTML: :width 640px
#+ATTR_ORG:  :width 100
[[./fig-03-scatter-bwt-lwt-race3-2.png]]

* La régression linéaire multiple

** Exemple de base

** Diagnostic du modèle

** Tests joints et intervalles de confiance simultanés

** Spécification de contrastes

** Comparaison de modèles emboîtés


** Ces des données en cluster

* Modèle linéaire et applications


#+BIBLIOGRAPHY: references nil limit:t option:-nobibsource

